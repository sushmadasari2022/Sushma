data "databricks_aws_assume_role_policy" "policy" {
  external_id = var.databricks_account_id
}

resource "aws_iam_role" "cross_account_role" {
  name               = "${local.prefix}-crossaccount"
  assume_role_policy = data.databricks_aws_assume_role_policy.policy.json
  tags               = var.tags
}

data "databricks_aws_crossaccount_policy" "policy" {
}

resource "aws_iam_role_policy" "policy" {
  name   = "${local.prefix}-policy"
  role   = aws_iam_role.cross_account_role.id
  policy = data.databricks_aws_crossaccount_policy.policy.json
}

resource "databricks_mws_credentials" "credentials" {
  provider         = databricks.mws
  account_id       = var.databricks_account_id
  role_arn         = aws_iam_role.cross_account_role.arn
  credentials_name = "${local.prefix}-creds"
  depends_on       = [aws_iam_role_policy.policy]
}